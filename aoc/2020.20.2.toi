equalArray|left right| equal {
    equal = 1
    l = 0
    len = len(left)
    while l < len {
        if [left]l <> [right]l {
            equal = 0
            exit function
        }
        l = l + 1
    }
}

left|tile| out {
    out = array()
    for row = [tile]r {
        push(out, [row]0)
    }
}

right|tile| out {
    out = array()
    right = len([tile]0) - 1
    for row = [tile]r {
        push(out, [row]right)
    }
}

top|tile| out {
    out = [tile]0
}

bottom|tile| out {
    out = [tile](len(tile)-1)
}

Tile{tile left right top bottom}
createTile|tile| out {
    out = Tile(tile, left(tile), right(tile), top(tile), bottom(tile))
}

reverse|chars| out {
    out = array()
    i = len(chars) - 1
    while i >= 0 {
        push(out, [chars]i)
        i = i - 1
    }
}

flipHorizontally|tile| out {
    out = array()
    for row = [tile]r {
        push(out, reverse(row))
    }
}

rotate90deg|tile| out {
    out = array()
    for row = [tile]r {
        push(out, array())
    }
    max = len([tile]0)-1
    r = max
    while r >= 0 {
        row = [tile]r
        for char = [row]col {
            [[out]col](max-r) = char
        }
        r = r - 1
    }
}

rotate180deg|tile| out {
    out = rotate90deg(rotate90deg(tile))
}

rotate270deg|tile| out {
    out = rotate180deg(rotate90deg(tile))
}

allProjections|tile| out {
    flipped = flipHorizontally(tile)
    out = array( //
        tile, //
        rotate90deg(tile), //
        rotate180deg(tile), //
        rotate270deg(tile), //
        flipped, //
        rotate90deg(flipped), //
        rotate180deg(flipped), //
        rotate270deg(flipped) //
    )
}

toTiles|projections| out {
    out = array()
    for projection = [projections]p {
        push(out, createTile(projection))
    }
}

tiles = map()

readingMode = 0 // 0 = title; 1 = data

currentTile = array()
tileId = 0

for line = [inputLines()]l {
    if readingMode == 0 { // reading title, e.g. "Tile 2311:"
        if l <> 0 {
            [tiles]tileId = toTiles(allProjections(currentTile))
        }
        tileId = [split([split(line, " ")]1, ":")]0
        currentTile = array()
        readingMode = 1
    } otherwise {
        if line == "" {
            readingMode = 0
            next iteration
        }

        chars = chars(line)
        push(currentTile, chars)
    }
}

Coords{x y}
toKey|coords| key {
    key = string(coords.x) _ "," _ string(coords.y)
}
fromKey|key| coords {
    parts = split(key, ",")
    coords = Coords(int([parts]0), int([parts]1))
}

grid = map(toKey(Coords(0, 0)), createTile(currentTile))

min = Coords(0, 0)
max = Coords(0, 0)

while len(tiles) > 0 {
    for projections = [tiles]leftTileId {
        fits = 0
        keys = keys(grid)
        k = 0
        while fits == 0 and k < len(keys) {
            key = [keys]k
            k = k + 1
            gridTile = [grid]key
            p = 0
            while fits == 0 and p < len(projections) {
                projection = [projections]p
                p = p + 1
                dx = 0
                dy = 0
                if equalArray(projection.left, gridTile.right) {
                    fits = 1
                    dx = 1
                }
                if equalArray(projection.right, gridTile.left) {
                    fits = 1
                    dx = 0-1
                }
                if equalArray(projection.top, gridTile.bottom) {
                    fits = 1
                    dy = 1
                }
                if equalArray(projection.bottom, gridTile.top) {
                    fits = 1
                    dy = 0-1
                }
                if fits == 0 {
                    next iteration
                }
                coords = fromKey(key)
                newCoords = Coords(coords.x + dx, coords.y + dy)
                if newCoords.x < min.x {
                    min.x = newCoords.x
                }
                if newCoords.x > max.x {
                    max.x = newCoords.x
                }
                if newCoords.y < min.y {
                    min.y = newCoords.y
                }
                if newCoords.y > max.y {
                    max.y = newCoords.y
                }
                newKey = toKey(newCoords)
                [grid]newKey = projection
                unset(tiles, leftTileId)
                exit loop
            }
        }
    }
}

tileWidth = len([currentTile]0)
tileHeight = len(currentTile)

image = array()
x = min.x
while x <= max.x {
    imageY = 0
    y = min.y
    while y <= max.y {
        tile = ([grid]toKey(Coords(x, y))).tile
        for row = [tile]r {
            if r == 0 or r == len(tile)-1 {
                next iteration
            }
            if x == min.x {
                [image]imageY = array()
            }
            for char = [row]c {
                if c <> 0 and c <> len(row)-1 {
                    push([image]imageY, char)
                }
            }
            imageY = imageY + 1
        }
        y = y + 1
    }
    x = x + 1
}

check|row pattern offset| checked {
    checked = 1
    for patternChar = [pattern]i {
        rowChar = [row](offset+i)
        if patternChar == "#" {
            if rowChar <> "#" {
                checked = 0
                exit function
            }
        }
    }
}

countOccurrences|row1 row2 row3 pattern1 pattern2 pattern3| count {
    count = 0
    offset = 0
    rowLen = len(row1)
    patternLen = len(pattern1)
    while offset+patternLen <= rowLen {
        if check(row1, pattern1, offset) and check(row2, pattern2, offset) and check(row3, pattern3, offset) {
            count = count + 1
        }

        offset = offset + 1
    }
}

findSeaMonsters|image head body legs| monsters {
    //head = array(18)
    //body = array(0, 5, 6, 11, 12, 17, 18, 19)
    //legs = array(1, 4, 7, 10, 13, 16)

    monsters = 0
    i = 2
    while i < len(image) {
        row1 = [image](i-2)
        row2 = [image](i-1)
        row3 = [image]i
        monsters = monsters + countOccurrences(row1, row2, row3, head, body, legs)
        i = i + 1
    }
}

head = chars("                  # ")
body = chars("#    ##    ##    ###")
legs = chars(" #  #  #  #  #  #   ")

for projection = [allProjections(image)]p {
    monsterCount = findSeaMonsters(projection, head, body, legs)
    if monsterCount <> 0 {
        hashes = 0
        for row = [projection]p {
            for char = [row]c {
                if char == "#" {
                    hashes = hashes + 1
                }
            }
        }
        println(hashes - monsterCount*15)


        exit loop
    }
}
