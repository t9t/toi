lines = inputLines()

memory = map()

for line = [lines]l {
    parts = split(line, " = ")
    if [parts]0 == "mask" {
        mask = chars([parts]1)
        power = 1
        set1bits = 0
        clearXBits = 0
        orMasks = array(0)
        i = len(mask)-1
        while i >= 0 {
            c = [mask]i
            if c == "X" {
                l = len(orMasks)
                j = 0
                while j < l {
                    orMask = [orMasks]j
                    j = j + 1
                    push(orMasks, orMask bor power)
                }
            } otherwise {
                clearXBits = clearXBits + power
            }
            if c == "1" {
                set1bits = set1bits + power
            }
            i = i - 1
            power = power * 2
        }

        next iteration
    }

    // "mem[nnn]"
    address = int([split([split([parts]0, "[")]1, "]")]0)
    value = int([parts]1)
    base = (address band clearXBits) bor set1bits
    for orMask = [orMasks]i {
        [memory](string(base bor orMask)) = value
    }
}

sum = 0
for value = [memory]address {
    sum = sum + value
}

println(sum)
