lines = inputLines()
rows = array()
for line = [lines]i {
    push(rows, chars(line))
}

rowsLen = len(rows)
rowLen = len([rows]0)

while 1 {
    changed = 0
    newRows = array()
    for row = [rows]r {
        newRow = array()
        for char = [row]c {
            adjacentOccupied = 0
            dr = 0 - 1
            while dr <= 1 {
                dc = 0 - 1
                while dc <= 1 {
                    if dr == 0 and dc == 0 {
                        dc = dc + 1
                        next iteration
                    }

                    d = 1
                    while 1 {
                        ddr = r + (d*dr)
                        ddc = c + (d*dc)

                        if ddr < 0 or ddc < 0 or ddr == rowsLen or ddc == rowLen {
                            // Reached the edge
                            exit loop
                        }

                        other = [[rows]ddr]ddc
                        if other == "#" {
                            adjacentOccupied = adjacentOccupied + 1
                            exit loop
                        }
                        if other == "L" {
                            // Obscured vision
                            exit loop
                        }

                        d = d + 1
                    }

                    dc = dc + 1
                }
                dr = dr + 1
            }

            newChar = char
            if char == "L" and adjacentOccupied == 0 {
                newChar = "#"
                changed = 1
            }
            if char == "#" and adjacentOccupied >= 5 {
                newChar = "L"
                changed = 1
            }
            push(newRow, newChar)
        }
        push(newRows, newRow)
    }

    rows = newRows
    if changed == 0 {
        exit loop
    }
}

occupied = 0
for row = [newRows]r {
    for char = [row]c {
        if char == "#" {
            occupied = occupied + 1
        }
    }
}
println(occupied)
