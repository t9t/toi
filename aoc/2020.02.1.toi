lines = inputLines()

i = 0
validCount = 0
while i < len(lines) {
    line = [lines]i
    i = i + 1

    parts = split(line, ": ") // "1-3 a" + "abcde"
    password = [parts]1 // "abcde"

    parts = split([parts]0, " ") // "1-3" + "a"
    policyLetter = [parts]1 // "a"

    parts = split([parts]0, "-") // "1" + "3"
    min = int([parts]0) // "1" -> 1
    max = int([parts]1) // "3" -> 3

    passwordChars = chars(password) // "abcde" -> ["a", "b", "c", "d", "e"]
    letterCount = 0
    j = 0
    while j < len(passwordChars) {
        if [passwordChars]j == policyLetter {
            letterCount = letterCount + 1
        }
        j = j + 1
    }

    if letterCount >= min & letterCount <= max {
        validCount = validCount + 1
    }
}

println(validCount)
