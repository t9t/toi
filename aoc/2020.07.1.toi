lines = inputLines()

containedBags = map()

i = 0
while i < len(lines) {
    line = get(lines, i)
    i = i + 1

    parts = split(line, " bags contain ")
    outerColor = get(parts, 0)
    otherBags = get(parts, 1)

    if otherBags <> "no other bags." {
        // e.g. "5 faded blue bags, 6 dotted black bags, N <color>."
        bags = split(get(split(otherBags, "."), 0), ", ")
        b = 0
        while b < len(bags) {
            words = split(get(bags, b), " ")
            b = b + 1

            // [5, faded, blue, bags]; [1, bright, white, bag]
            color = get(words, 1) _ " " _ get(words, 2)

            if isSet(containedBags, color) {
                entry = get(containedBags, color)
            } otherwise {
                entry = array()
                set(containedBags, color, entry)
            }
            push(entry, outerColor)
        }
    }
}

search = array()
push(search, "shiny gold")
hits = map()

while len(search) {
    color = pop(search)

    if isSet(containedBags, color) {
        bags = get(containedBags, color)
        i = 0
        while i < len(bags) {
            bag = get(bags, i)
            i = i + 1
            push(search, bag)
            set(hits, bag, 1)
        }
    }
}

println(len(hits))
