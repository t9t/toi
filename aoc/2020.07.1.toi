containedBags = map()

for line = [inputLines()]l {
    parts = split(line, " bags contain ")
    outerColor = [parts]0
    otherBags = [parts]1

    if otherBags <> "no other bags." {
        // e.g. "5 faded blue bags, 6 dotted black bags, N <color>."
        for bag = [split([split(otherBags, ".")]0, ", ")]b {
            words = split(bag, " ")

            // [5, faded, blue, bags]; [1, bright, white, bag]
            color = [words]1 _ " " _ [words]2

            if isSet(containedBags, color) {
                entry = [containedBags]color
            } otherwise {
                entry = array()
                [containedBags]color = entry
            }
            push(entry, outerColor)
        }
    }
}

search = array()
push(search, "shiny gold")
hits = map()

while len(search) {
    color = pop(search)

    if isSet(containedBags, color) {
        for bag = [[containedBags]color]i {
            push(search, bag)
            [hits]bag = 1
        }
    }
}

println(len(hits))
